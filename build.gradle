buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.2'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.0' apply false
}

allprojects {
    apply plugin: "java"
    apply plugin: "kotlin"
}

group 'chrislo27'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

task coreJar(type: Jar, dependsOn: project(":main").build) {
    classifier = "core"
    from project(":main").sourceSets.main.output
}

task guiJar(type: Jar, dependsOn: [project(":main"), project(":gui")]*.build) {
    classifier = "gui"
    from project(":main").sourceSets.main.output
    from project(":gui").sourceSets.main.output
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
    from subprojects*.sourceSets.main.allSource
}

artifacts {
    archives coreJar
    archives guiJar
    archives sourcesJar
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib"
    }
}

subprojects {
    sourceSets.main.java.srcDirs += "src/main/kotlin/"
}

project(":main") {
    test {
        useJUnitPlatform()
    }
    dependencies {
        implementation "com.fasterxml.jackson.module:jackson-module-afterburner:${jackson_version}"
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin:${jackson_version}"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0"
        implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    }
}

project(":gui") {
    dependencies {
        implementation project(":main")
        implementation "org.apache.logging.log4j:log4j-core:${log4j_version}"
        implementation "org.apache.logging.log4j:log4j-api:${log4j_version}"
        implementation "com.github.MinnDevelopment:java-discord-rpc:v2.0.1"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}